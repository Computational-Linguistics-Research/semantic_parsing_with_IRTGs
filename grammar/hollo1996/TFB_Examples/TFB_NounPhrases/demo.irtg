interpretation tree: de.up.ling.irtg.algebra.TagTreeAlgebra
interpretation ud: de.up.ling.irtg.algebra.graph.GraphAlgebra
interpretation fourlang: de.up.ling.irtg.algebra.graph.GraphAlgebra

//UPPER NODES
//UPPER NODES

//entry point
S! -> sentence(NP)
[tree] ?1
[ud] ?1
[fourlang] ?1




//LOWER NODES
//LOWER NODES

//Noun_Phrases
//Noun_Phrases

//unary
//unary

//NounPhrase out of one Determinant
//NP( DT( a))
//(NP (DT a))
NP -> one_DT(DT)
[tree] NP(?1)
[ud] ?1
[fourlang] ?1

//NounPhrase out of one Adjective
//NP( JJ( big))
//(NP (JJ big))
NP -> one_JJ(JJ)
[tree] NP(?1)
[ud] ?1
[fourlang] ?1

//NounPhrase out of one Noun
//NP( NN( cat))
//(NP (NN cat))
NP -> one_NN(NNbasic)
[tree] NP(?1)
[ud] ?1
[fourlang] ?1


//binary
//binary

//NounPhrase out of a Determinant (whitch is a Determinant) and an Adjective.
//NP2( DT( a), JJ( big))
//(NP (DT a) (JJ big))
NP -> det_DT_JJ(DT,JJbasic)
[tree] NP2(?1,?2)
[ud] merge(f_dep(merge("(r<root> :det (d<dep>))",r_dep(?1))),?2)
[fourlang] ?2

//NounPhrase out of a Determinant (whitch is a Determinant) and a Noun.
//NP2( DT( a), NN( cat))
//(NP (DT a) (NN cat))
NP -> det_DT_NN(DT,NNbasic)
[tree] NP2(?1,?2)
[ud] merge(f_dep(merge("(r<root> :det (d<dep>))",r_dep(?1))),?2)
[fourlang] ?2

//NounPhrase out of an Adjective (whitch is an AdjectivalModifier) and a Noun.
//NP2( JJ( big), NN( cat))
//(NP (JJ big) (NN cat))
NP -> amod_JJ_NN(JJbasic,NNbasic)
[tree] NP2(?1,?2)
[ud] merge(f_dep(merge("(r<root> :amod (d<dep>))",r_dep(?1))),?2)
[fourlang] merge(f_dep(merge("(r<root> :0 (d<dep>))",r_dep(?1))),?2)


//ternary
//ternary

//complete
//

//NounPhrase starting with a DT (whitch is a Determiner).
//NP3( DT( a), JJ( big), NN( cat))
//(NP (DT a) (JJ big) (NN cat))
NP -> det_DT_NBAR(DT,N_BAR)
[tree] @(?2,?1)
[ud] merge(f_dep(merge("(r<root> :det (d<dep>))",r_dep(?1))),?2)
[fourlang] ?2

//tail
//

//NounBar Out of a Adjective (whitch is an adjectival modifier) and a Noun.
//NP3( ...( ...), JJ( big), NN( cat))
//(NP (... ...) (JJ big) (NN cat))
N_BAR -> amod_JJ_NN_Part(JJbasic,NNbasic)
[tree] NP3(*,?1,?2)
[ud] merge(f_dep(merge("(r<root> :amod (d<dep>))", r_dep(?1))),?2)
[fourlang] merge(f_dep(merge("(r<root> :0 (d<dep>))", r_dep(?1))),?2)




//TERMINALS
//TERMINALS

//Converters
//Converters

//Nouns
//Nouns

//basic
NN -> NNbasic_2_NN(NNbasic)
[tree] ?1
[ud] ?1
[fourlang] ?1

//proper
NN -> NNP_2_NN(NNP)
[tree] ?1
[ud] ?1
[fourlang] ?1

//plural
NN -> NNS_2_NN(NNS)
[tree] ?1
[ud] ?1
[fourlang] ?1

//proper plural
NN -> NNPS_2_NN(NNPS)
[tree] ?1
[ud] ?1
[fourlang] ?1


//Adjectives
//Adjectives

//basic
JJ -> JJbasic_2_JJ(JJbasic)
[tree] ?1
[ud] ?1
[fourlang] ?1

//comparative
JJ -> JJR_2_JJ(JJR)
[tree] ?1
[ud] ?1
[fourlang] ?1

//superlative
JJ -> JJS_2_JJ(JJS)
[tree] ?1
[ud] ?1
[fourlang] ?1


//Dictionary
//Dictionary

//Noun
//cat
NNbasic -> cat_NN
[tree] NN(cat)
[ud] "(cat<root>/cat)"
[fourlang] "(cat<root>/cat)"

//Determinant
//a
DT -> a_DT
[tree] DT(a)
[ud] "(a<root>/a)"
[fourlang] "(a<root>/a)"

//Adjective
//big
JJbasic -> big_JJ
[tree] JJ(big)
[ud] "(big<root>/big)"
[fourlang] "(big<root>/big)"
